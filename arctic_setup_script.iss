; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "ЯМАЛ-АРКТИКА"
#define MyAppVersion "1.0.0.1"
#define MyAppPublisher "Interregional Expedition Center 'Arctic'"
#define MyAppURL "http://iec-arctic.ru/"
#define MyAppExeName "ArcticDB.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A1AF76D5-235C-4D0B-9C3C-607A89FB123F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={sd}\ArcticDB
DefaultGroupName={#MyAppName}
LicenseFile=lisc.txt
OutputDir=output
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
ShowLanguageDialog=no
LanguageDetectionMethod=none
VersionInfoVersion=1.0.0.0
MinVersion=0,5.01

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}";
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; OnlyBelowVersion: 0,6.1

[Files]
Source: "ArcticDB\bin\Release\ArcticDB.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\ReportsArctic\*"; DestDir: "{app}\ReportsArctic"; Flags: ignoreversion recursesubdirs createallsubdirs onlyifdoesntexist
Source: "ArcticDB\bin\Release\x64\*"; DestDir: "{app}\x64"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "ArcticDB\bin\Release\x86\*"; DestDir: "{app}\x86"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "ArcticDB\extra\7za.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\extra\arctic.db"; DestDir: "{app}"; Flags: ignoreversion onlyifdoesntexist
Source: "ArcticDB\bin\Release\ArcticDB.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\ArcticDB.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\ArcticDB.pdb"; DestDir: "{app}"; Flags: ignoreversion
;Source: "ArcticDB\bin\Release\ArcticDB.vshost.exe"; DestDir: "{app}"; Flags: ignoreversion
;Source: "ArcticDB\bin\Release\ArcticDB.vshost.exe.config"; DestDir: "{app}"; Flags: ignoreversion
;Source: "ArcticDB\bin\Release\ArcticDB.vshost.exe.manifest"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\extra\DBExportList.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\EntityFramework.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\EntityFramework.SqlServer.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\EntityFramework.SqlServer.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\EntityFramework.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\System.Data.SQLite.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\System.Data.SQLite.EF6.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\System.Data.SQLite.Linq.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\System.Data.SQLite.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\extra\warning.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\extra\dotNetFx40_Full_x86_x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall; Check: not IsRequiredDotNetDetected
Source: "ArcticDB\bin\Release\NLog.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\NLog.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ArcticDB\bin\Release\NLog.xml"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[CustomMessages]
myCustomMessage=На компьютере не установлена Microsoft .NET Framework 4.0 Full Программа попытается его установить в конце установки

[Code]
//-----------------------------------------------------------------------------
//  Проверка наличия нужного фреймворка
//-----------------------------------------------------------------------------
function IsDotNetDetected(version: string; release: cardinal): boolean;

var 
    reg_key: string; // Просматриваемый подраздел системного реестра
    success: boolean; // Флаг наличия запрашиваемой версии .NET
    release45: cardinal; // Номер релиза для версии 4.5.x
    key_value: cardinal; // Прочитанное из реестра значение ключа
    sub_key: string;

begin

    success := false;
    reg_key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\';
    
    // Вресия 3.0
    if Pos('v3.0', version) = 1 then
      begin
          sub_key := 'v3.0';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'InstallSuccess', key_value);
          success := success and (key_value = 1);
      end;

    // Вресия 3.5
    if Pos('v3.5', version) = 1 then
      begin
          sub_key := 'v3.5';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Install', key_value);
          success := success and (key_value = 1);
      end;

     // Вресия 4.0 клиентский профиль
     if Pos('v4.0 Client Profile', version) = 1 then
      begin
          sub_key := 'v4\Client';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Install', key_value);
          success := success and (key_value = 1);
      end;

     // Вресия 4.0 расширенный профиль
     if Pos('v4.0 Full Profile', version) = 1 then
      begin
          sub_key := 'v4\Full';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Install', key_value);
          success := success and (key_value = 1);
      end;

     // Вресия 4.5
     if Pos('v4.5', version) = 1 then
      begin
          sub_key := 'v4\Full';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Release', release45);
          success := success and (release45 >= release);
      end;
        
    result := success;

end;

//-----------------------------------------------------------------------------
//  Функция-обертка для детектирования конкретной нужной нам версии
//-----------------------------------------------------------------------------
function IsRequiredDotNetDetected(): boolean;
begin
    result := IsDotNetDetected('v4.0 Full Profile', 0);
end;

//-----------------------------------------------------------------------------
//    Callback-функция, вызываемая при инициализации установки
//-----------------------------------------------------------------------------
function InitializeSetup(): boolean;
  // Если нет тербуемой версии .NET выводим сообщение о том, что инсталлятор
  // попытается установить её на данный компьютер
begin
  if not IsDotNetDetected('v4.0 Full Profile', 0) then
     begin
        MsgBox(ExpandConstant('{cm:myCustomMessage}'), mbInformation, MB_OK);
      end; 

  result := true;
end;

[Run]
Filename: "{tmp}\dotNetFx40_Full_x86_x64.exe"; Parameters: "/q:a /c:""install /l /q"""; Check: not IsRequiredDotNetDetected; StatusMsg: Microsoft Framework 4.0 Устанавливается, пожалуйста подождите 
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[LangOptions]
LanguageID=$0419
DialogFontName=Arial
DialogFontSize=10
